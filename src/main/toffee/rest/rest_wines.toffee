###global JSON,module,require,console,process###
debug           = require("debug")("rest_wines")
wine            = require "../model/wine"
mongoose        = require "mongoose"
mongooseHidden  = require("mongoose-hidden")()
restify         = require "restify"
restifyMongoose = require "restify-mongoose"
mongoUrl        = process.env.MONGODB_URL or "mongodb://localhost:27017/test"
protocol        = process.env.PROTOCOL or "http"
host            = process.env.HOST or "localhost"
port            = process.env.PORT
nodeUrl         = if port then "#{protocol}://#{host}:#{port}" else "#{protocol}://#{host}"

module.exports  =
    register: (server, path) ->
        path = path or ""

        mongoose.connect mongoUrl
        schema = new mongoose.Schema(wine.schema)
        schema.plugin mongooseHidden, defaultHidden:
            _id: true
            __v: true
        Wine = mongoose.model("wines", schema)

        filter = (req) ->
            query = {}
            if req.query.name    then query.name    = req.query.name
            if req.query.year    then query.year    = req.query.year
            if req.query.type    then query.type    = req.query.type
            if req.query.country then query.country = req.query.country
            return query
        options = baseUrl: nodeUrl, queryString: "id", filter: filter
        wines = restifyMongoose Wine, options
        server.get  path + "/wines",     wines.query()
        server.get  path + "/wines/:id", wines.detail()
        server.post path + "/wines",     wines.insert()
        server.put  path + "/wines/:id", wines.update()
        server.del  path + "/wines/:id", wines.remove()

        return this
